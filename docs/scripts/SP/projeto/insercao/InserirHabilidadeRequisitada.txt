CREATE PROCEDURE sp_InserirHabilidadeRequisitada
    @DiscenteCursoInstituicaoID UNIQUEIDENTIFIER,
    @ProjetoID UNIQUEIDENTIFIER,
    @HabilidadeID UNIQUEIDENTIFIER,
    @NivelHabilidadeID UNIQUEIDENTIFIER
AS
BEGIN
    SET NOCOUNT ON;
    SET XACT_ABORT ON;

    DECLARE @Validacao INT;

    EXEC @Validacao = sp_ValidarDiscenteCursoInstituicao
        @DiscenteCursoInstituicaoID; 
    
    IF @Validacao <> 0 RETURN @Validacao;


    EXEC @Validacao = sp_ValidarHabilidadeRequisitada
        @DiscenteCursoInstituicaoID,
        @ProjetoID,
        @HabilidadeID,
        @NivelHabilidadeID;
    
    IF @Validacao <> 0 RETURN @Validacao;
    
    BEGIN TRY
        DECLARE @OutputID TABLE(
            NovoID UNIQUEIDENTIFIER
        );

        BEGIN TRANSACTION
            IF NOT EXISTS(
                SELECT 
                    1
                FROM
                    HABILIDADE_REQUISITADA
                WITH(
                    UPDLOCK,
                    HOLDLOCK
                )
                WHERE
                    ID_PROJETO = @ProjetoID
                AND
                    ID_HABILIDADE = @HabilidadeID
            )
            BEGIN
                INSERT INTO
                    HABILIDADE_REQUISITADA(
                        ID_PROJETO,
                        ID_HABILIDADE,
                        ID_NIVEL_HABILIDADE
                    )
                    OUTPUT 
                        inserted.ID_HABILIDADE_REQUISITADA
                    INTO
                        @OutputID
                    VALUES(
                        @ProjetoID,
                        @HabilidadeID,
                        @NivelHabilidadeID
                );
            END

            ELSE
            BEGIN
                INSERT INTO @OutputID(
                    NovoID
                )
                SELECT
                    TOP 1
                    ID_HABILIDADE_REQUISITADA
                FROM
                    HABILIDADE_REQUISITADA
                WHERE
                    ID_PROJETO = @ProjetoID
                AND
                    ID_HABILIDADE = @HabilidadeID
            END
        COMMIT TRANSACTION;

        SELECT
            NovoID
        FROM
            @OutputID;

    END TRY
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION;
        EXEC sp_TratarErro;
        THROW;
    END CATCH
END

/*
EXEC sp_InserirHabilidadeRequisitada
    @DiscenteCursoInstituicaoID = 'x',
    @ProjetoID = 'y',
    @HabilidadeID = 'z',
    @NivelHabilidadeID = 'a';
*/