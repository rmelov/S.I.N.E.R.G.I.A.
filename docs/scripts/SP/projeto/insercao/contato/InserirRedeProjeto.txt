CREATE PROCEDURE sp_InserirRedeProjeto
    @DiscenteCursoInstituicaoID UNIQUEIDENTIFIER,
    @ProjetoID UNIQUEIDENTIFIER,
    @RedeID UNIQUEIDENTIFIER,
    @URL VARCHAR(500)
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE @Validacao INT;

    EXEC @Validacao = sp_ValidarDiscenteCursoInstituicao
        @DiscenteCursoInstituicaoID; 
    
    IF @Validacao <> 0 RETURN;

    DECLARE @AutoriaPrincipalID UNIQUEIDENTIFIER;
    DECLARE @CoAutoriaID UNIQUEIDENTIFIER;

    EXEC @Validacao = sp_OrdensAutoriaID
        @AutoriaPrincipalID = @AutoriaPrincipalID OUTPUT,
        @CoAutoriaID = @CoAutoriaID OUTPUT;

    IF @Validacao <> 0 RETURN;

    EXEC @Validacao = sp_ValidarProjetoPermissaoGestao
        @DiscenteCursoInstituicaoID,
        @ProjetoID,
        @AutoriaPrincipalID,
        @CoAutoriaID;

    IF @Validacao <> 0 RETURN;


    SET @URL = LOWER(TRIM(@URL));

    EXEC @Validacao = sp_ValidarUrlRede
        @RedeID,
        @URL;

    IF @Validacao <> 0 RETURN;


    IF EXISTS(
        SELECT
            1
        FROM
            REDE_DISCENTE
        WHERE
            URL_REDE = @URL
        AND
            ID_REDE = @RedeID
    )
    BEGIN
        IF NOT EXISTS(
            SELECT
                1 
            FROM
                REDE_DISCENTE RD
            INNER JOIN
                DISCENTE_CURSO_INSTITUICAO DCI
            ON
                RD.ID_DISCENTE = DCI.ID_DISCENTE
            INNER JOIN
                AUTORIA_PROJETO AP
            ON
                DCI.ID_DISCENTE_CURSO_INSTITUICAO = AP.ID_DISCENTE_CURSO_INSTITUICAO
            WHERE
                RD.URL_REDE = @URL 
            AND
                RD.ID_REDE = @RedeID
            AND
                AP.ID_PROJETO = @ProjetoID
        )
        BEGIN
        RAISERROR(
            'Não foi possível vincular este link, pois não é de ninguém parte deste projeto.', 
            16, 
            1
        );
        RETURN;
        END
    END

    BEGIN TRY
        BEGIN TRANSACTION
            IF NOT EXISTS(
                SELECT
                    1
                FROM
                    REDE_PROJETO
                WHERE
                    ID_PROJETO = @ProjetoID
                AND
                    ID_REDE = @RedeID
                AND
                    URL_REDE = @URL
            )
            BEGIN
                INSERT INTO
                    REDE_PROJETO(
                        ID_PROJETO,
                        ID_REDE,
                        URL_REDE                        
                )
                VALUES(
                    @ProjetoID,
                    @RedeID,
                    @URL
                );
            END
        COMMIT TRANSACTION;

        SELECT 
            ID_REDE_PROJETO
        FROM
            REDE_PROJETO
        WHERE
            ID_PROJETO = @ProjetoID
            AND
                ID_REDE = @RedeID
            AND
                URL_REDE = @URL;
    END TRY

    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION;
        EXEC sp_TratarErro;
        THROW;
    END CATCH
END;

/*
EXEC sp_InserirRedeProjeto
    @DiscenteCursoInstituicaoID = 'x',
    @ProjetoID = 'y',
    @RedeID = 'z',
    @URL = 'a';
*/