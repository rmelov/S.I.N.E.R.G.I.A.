CREATE PROCEDURE sp_InserirTelefoneProjeto
    @DiscenteCursoInstituicaoID UNIQUEIDENTIFIER,
    @ProjetoID UNIQUEIDENTIFIER,
    @TELEFONE VARCHAR(500)
AS
BEGIN
    SET NOCOUNT ON;

    
    DECLARE @Validacao INT;

    EXEC @Validacao = sp_ValidarDiscenteCursoInstituicao
        @DiscenteCursoInstituicaoID; 
    
    IF @Validacao <> 0 RETURN;

    DECLARE @AutoriaPrincipalID UNIQUEIDENTIFIER;
    DECLARE @CoAutoriaID UNIQUEIDENTIFIER;

    EXEC @Validacao = sp_OrdensAutoriaID
        @AutoriaPrincipalID = @AutoriaPrincipalID OUTPUT,
        @CoAutoriaID = @CoAutoriaID OUTPUT;

    IF @Validacao <> 0 RETURN;

    EXEC @Validacao = sp_ValidarProjetoPermissaoGestao
        @DiscenteCursoInstituicaoID,
        @ProjetoID,
        @AutoriaPrincipalID,
        @CoAutoriaID;

    IF @Validacao <> 0 RETURN;


    SET @TELEFONE = TRIM(@TELEFONE);

    EXEC @Validacao = sp_ValidarTelefone
        @TELEFONE;

    IF @Validacao <> 0 RETURN;


    DECLARE @NovoTelefoneID UNIQUEIDENTIFIER;
    DECLARE @TelefoneID UNIQUEIDENTIFIER;
    DECLARE @TelefoneDiscenteID UNIQUEIDENTIFIER;

    SELECT
        @TelefoneID = T.ID_TELEFONE,
        @TelefoneDiscenteID = TD.ID_TELEFONE
    FROM
        TELEFONE T
    LEFT JOIN
        TELEFONE_DISCENTE TD
    ON
        TD.ID_TELEFONE = T.ID_TELEFONE
    WHERE
        T.NUMERO_TELEFONE = @TELEFONE;

    IF @TelefoneDiscenteID IS NOT NULL
    BEGIN
        IF NOT EXISTS(
            SELECT 
                1
            FROM
                AUTORIA_PROJETO AP
            INNER JOIN
                DISCENTE_CURSO_INSTITUICAO DCI
            ON
                AP.ID_DISCENTE_CURSO_INSTITUICAO = DCI.ID_DISCENTE_CURSO_INSTITUICAO
            LEFT JOIN
                TELEFONE_DISCENTE TD
            ON
                TD.ID_DISCENTE = DCI.ID_DISCENTE
            WHERE
                AP.ID_PROJETO = @ProjetoID
            AND
                TD.ID_TELEFONE = @TelefoneID
        )
        BEGIN
            RAISERROR(
                'Não foi possível vincular este telefone, pois não é de ninguém parte deste projeto.', 
                16, 
                1
            );
            RETURN;
        END
    END

    BEGIN TRY
        BEGIN TRANSACTION            
            IF @TelefoneID IS NOT NULL
                SET @NovoTelefoneID = @TelefoneID;
            ELSE
                EXEC sp_InserirTelefone 
                    @TELEFONE = @TELEFONE, 
                    @TELEFONE_ID = @NovoTelefoneID OUTPUT;           

            IF NOT EXISTS(
                SELECT
                    1
                FROM
                    TELEFONE_PROJETO 
                WHERE
                    ID_PROJETO = @ProjetoID
                AND
                    ID_TELEFONE = @NovoTelefoneID
            )
            BEGIN
                INSERT INTO
                    TELEFONE_PROJETO(
                        ID_TELEFONE,
                        ID_PROJETO
                )
                VALUES(
                    @NovoTelefoneID,
                    @ProjetoID
                );
            END
        COMMIT TRANSACTION;

        SELECT 
            ID_TELEFONE_PROJETO 
        FROM 
            TELEFONE_PROJETO 
        WHERE 
            ID_PROJETO = @ProjetoID 
        AND
            ID_TELEFONE = @NovoTelefoneID;
    END TRY

    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION;
        EXEC sp_TratarErro;
        THROW;
    END CATCH
END;

/*
EXEC sp_InserirTelefoneProjeto
    @DiscenteCursoInstituicaoID = 'x',
    @ProjetoID = 'y'
    @TELEFONE = '+5512981012345';
*/