CREATE PROCEDURE sp_InserirOrdemAutoria
    @DESIGNACAO NVARCHAR(500),
    @HIERARQUIA INT
AS
BEGIN
    SET NOCOUNT ON;
    SET XACT_ABORT ON;

    SET @DESIGNACAO = UPPER(TRIM(@DESIGNACAO));

    DECLARE @Validacao INT;

    EXEC @Validacao = sp_ValidarOrdemAutoria
        @DESIGNACAO,
        @HIERARQUIA;

    IF @Validacao <> 0 RETURN @Validacao;

    BEGIN TRY
        DECLARE @OutputID TABLE(
            NovoID UNIQUEIDENTIFIER
        );

        BEGIN TRANSACTION;          
            IF NOT EXISTS(
                SELECT 
                    1
                FROM
                    ORDEM_AUTORIA
                WITH(
                    UPDLOCK,
                    HOLDLOCK
                )
                WHERE
                    DESIGNACAO_ORDEM_AUTORIA = @DESIGNACAO
                OR
                    HIERARQUIA_ORDEM_AUTORIA = @HIERARQUIA
            )
            BEGIN
                INSERT INTO ORDEM_AUTORIA(
                    DESIGNACAO_ORDEM_AUTORIA,
                    HIERARQUIA_ORDEM_AUTORIA
                )
                OUTPUT
                    inserted.ID_ORDEM_AUTORIA
                INTO
                    @OutputID
                VALUES(
                    @DESIGNACAO,
                    @HIERARQUIA
                );
            END

            ELSE
            BEGIN
                INSERT INTO @OutputID(
                    NovoID
                )
                SELECT
                    TOP 1
                    ID_ORDEM_AUTORIA
                FROM
                    ORDEM_AUTORIA
                WHERE
                    DESIGNACAO_ORDEM_AUTORIA = @DESIGNACAO
                OR
                    HIERARQUIA_ORDEM_AUTORIA = @HIERARQUIA;
            END

        COMMIT TRANSACTION;

        SELECT
            NovoID
        FROM
            @OutputID;
            
    END TRY
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION;
        EXEC sp_TratarErro;
        THROW;
    END CATCH

END;

/*
EXEC sp_InserirOrdemAutoria
    @DESIGNACAO = 'autoria principal',
    @HIERARQUIA = 1;
*/