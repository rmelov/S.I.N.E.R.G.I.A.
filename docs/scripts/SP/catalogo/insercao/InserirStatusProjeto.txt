CREATE PROCEDURE sp_InserirStatusProjeto
    @DESIGNACAO NVARCHAR(500),
    @HIERARQUIA INT
AS
BEGIN
    SET NOCOUNT ON;
    SET XACT_ABORT ON;

    SET @DESIGNACAO = UPPER(TRIM(@DESIGNACAO));

    DECLARE @Validacao INT;

    EXEC @Validacao = sp_ValidarStatusProjeto
        @DESIGNACAO,
        @HIERARQUIA;

    IF @Validacao <> 0 RETURN @Validacao;

    BEGIN TRY
        DECLARE @OutputID TABLE(
            NovoID UNIQUEIDENTIFIER
        );

        BEGIN TRANSACTION;
            IF NOT EXISTS(
                SELECT 
                    1
                FROM
                    STATUS_PROJETO
                WITH(
                    UPDLOCK,
                    HOLDLOCK
                )
                WHERE
                    DESIGNACAO_STATUS_PROJETO = @DESIGNACAO
                OR
                    HIERARQUIA_STATUS_PROJETO = @HIERARQUIA
            )
            BEGIN     
                INSERT INTO STATUS_PROJETO(
                    DESIGNACAO_STATUS_PROJETO,
                    HIERARQUIA_STATUS_PROJETO
                )
                OUTPUT
                    inserted.ID_STATUS_PROJETO
                INTO
                    @OutputID
                VALUES(
                    @DESIGNACAO,
                    @HIERARQUIA
                );
            END

            ELSE
            BEGIN
                INSERT INTO @OutputID(
                    NovoID
                )
                SELECT
                    TOP 1
                    ID_STATUS_PROJETO
                FROM
                    STATUS_PROJETO
                WHERE
                    DESIGNACAO_STATUS_PROJETO = @DESIGNACAO
                OR
                    HIERARQUIA_STATUS_PROJETO = @HIERARQUIA;
            END

        COMMIT TRANSACTION;

        SELECT
            NovoID
        FROM
            @OutputID;
            
    END TRY
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION;
        EXEC sp_TratarErro;
        THROW;
    END CATCH

END;

/*
EXEC sp_InserirStatusProjeto
    @DESIGNACAO = 'em busca de discente',
    @HIERARQUIA = 1;
*/