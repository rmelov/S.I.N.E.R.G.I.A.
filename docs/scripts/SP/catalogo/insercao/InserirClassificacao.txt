CREATE PROCEDURE sp_InserirClassificacao
    @DESIGNACAO NVARCHAR(500),
    @DiscenteCursoInstituicaoID UNIQUEIDENTIFIER
AS
BEGIN
    SET NOCOUNT ON;
    SET XACT_ABORT ON;

    SET @DESIGNACAO = UPPER(TRIM(@DESIGNACAO));

    DECLARE @Validacao INT;

    EXEC @Validacao = sp_ValidarDiscenteCursoInstituicao
        @DiscenteCursoInstituicaoID; 
    
    IF @Validacao <> 0 RETURN @Validacao;

    EXEC @Validacao = sp_ValidarClassificacao
        @DESIGNACAO;

    IF @Validacao <> 0 RETURN @Validacao;


    DECLARE @DiscenteID UNIQUEIDENTIFIER;

    EXEC @Validacao = sp_DiscenteID
        @DiscenteCursoInstituicaoID,
        @DiscenteID OUTPUT;

    IF @Validacao <> 0 RETURN @Validacao;

    BEGIN TRY
        DECLARE @OutputID TABLE(
            NovoID UNIQUEIDENTIFIER
        );
        BEGIN TRANSACTION;             
            IF NOT EXISTS(
                SELECT 
                    1
                FROM
                    CLASSIFICACAO
                WITH(
                    UPDLOCK,
                    HOLDLOCK
                )
                WHERE
                    DESIGNACAO_CLASSIFICACAO = @DESIGNACAO
            )
            BEGIN
                INSERT INTO CLASSIFICACAO(
                    DESIGNACAO_CLASSIFICACAO,
                    ID_DISCENTE
                )
                OUTPUT
                    inserted.ID_CLASSIFICACAO
                INTO
                    @OutputID
                VALUES(
                    @DESIGNACAO,
                    @DiscenteID
                    
                );
            END

            ELSE
            BEGIN
                INSERT INTO @OutputID(
                    NovoID
                )
                SELECT
                    TOP 1
                    ID_CLASSIFICACAO 
                FROM
                    CLASSIFICACAO 
                WHERE
                    DESIGNACAO_CLASSIFICACAO = @DESIGNACAO;
            END

        COMMIT TRANSACTION;

        SELECT
            NovoID
        FROM
            @OutputID;
    END TRY
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION;
        EXEC sp_TratarErro;
        THROW;
    END CATCH
END;

/*
EXEC sp_InserirClassificacao
    @DESIGNACAO = 'portif√≥lio',
    @DiscenteCursoInstituicaoID = 'x';
*/