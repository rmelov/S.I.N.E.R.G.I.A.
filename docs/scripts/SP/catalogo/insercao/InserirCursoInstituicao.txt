CREATE PROCEDURE sp_InserirCursoInstituicao
    @CursoID UNIQUEIDENTIFIER,
    @InstituicaoID UNIQUEIDENTIFIER
AS
BEGIN
    SET NOCOUNT ON;
    SET XACT_ABORT ON;

    DECLARE @Validacao INT;

    EXEC @Validacao = sp_ValidarCursoInstituicao
        @CursoID,
        @InstituicaoID; 
    
    IF @Validacao <> 0 RETURN @Validacao;

    BEGIN TRY
        DECLARE @OutputID TABLE(
            NovoID UNIQUEIDENTIFIER
        );

        BEGIN TRANSACTION;
            IF NOT EXISTS(
                SELECT
                    1
                FROM
                    CURSO_INSTITUICAO
                WITH(
                    UPDLOCK,
                    HOLDLOCK
                )
                WHERE
                    ID_CURSO = @CursoID
                AND
                    ID_INSTITUICAO = @InstituicaoID
            )
            BEGIN
                INSERT INTO CURSO_INSTITUICAO(
                    ID_CURSO, 
                    ID_INSTITUICAO
                )
                OUTPUT
                    inserted.ID_CURSO_INSTITUICAO
                INTO
                    @OutputID
                VALUES(
                    @CursoID,
                    @InstituicaoID
                );
            END

            ELSE
            BEGIN
                INSERT INTO @OutputID(
                    NovoID
                )
                SELECT
                    TOP 1
                    ID_CURSO_INSTITUICAO
                FROM
                    CURSO_INSTITUICAO
                WHERE
                    ID_CURSO = @CursoID
                AND
                    ID_INSTITUICAO = @InstituicaoID
            END
        COMMIT TRANSACTION;

        SELECT
            NovoID
        FROM
            @OutputID;
        
    END TRY
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION;
        EXEC sp_TratarErro;
        THROW;
    END CATCH
END;

/*
EXEC sp_InserirCursoInstituicao
    @CursoID = 'x',
    @InstituicaoID = 'y';
*/