CREATE PROCEDURE sp_InserirRede
    @DESIGNACAO NVARCHAR(500),
    @DOMINIO VARCHAR(500),
    @DiscenteCursoInstituicaoID UNIQUEIDENTIFIER
AS
BEGIN
    SET NOCOUNT ON;
    SET XACT_ABORT ON;

    SET @DESIGNACAO = TRIM(@DESIGNACAO);
    SET @DOMINIO = LOWER(TRIM(@DOMINIO));

    DECLARE @Validacao INT;

    EXEC @Validacao = sp_ValidarDiscenteCursoInstituicao
        @DiscenteCursoInstituicaoID; 
    
    IF @Validacao <> 0 RETURN @Validacao;


    EXEC @Validacao = sp_ValidarRede
        @DESIGNACAO,
        @DOMINIO;
    IF @Validacao <> 0 RETURN @Validacao;

   
    DECLARE @DiscenteID UNIQUEIDENTIFIER;

    EXEC @Validacao = sp_DiscenteID
        @DiscenteCursoInstituicaoID,
        @DiscenteID OUTPUT;

    IF @Validacao <> 0 RETURN @Validacao;

    BEGIN TRY
        DECLARE @OutputID TABLE(
            NovoID UNIQUEIDENTIFIER
        );

        BEGIN TRANSACTION;
            IF NOT EXISTS(
                SELECT
                    1 
                FROM
                    REDE
                WITH(
                    UPDLOCK,
                    HOLDLOCK
                )
                WHERE
                    DESIGNACAO_REDE = @DESIGNACAO 
                OR
                    NOME_DOMINIO_REDE = @DOMINIO
            )
            BEGIN
                INSERT INTO REDE(
                    DESIGNACAO_REDE,
                    NOME_DOMINIO_REDE,
                    ID_DISCENTE
                )
                OUTPUT
                    inserted.ID_REDE
                INTO
                    @OutputID
                VALUES(
                    @DESIGNACAO,
                    @DOMINIO,
                    @DiscenteID
                );

                SELECT
                    @REDE_ID = NovoID
                FROM
                    @OutputID;
            END

            ELSE
            BEGIN
                INSERT INTO @OutputID(
                    NovoID
                )
                SELECT
                    TOP 1
                    ID_REDE 
                FROM
                    REDE 
                WHERE
                    DESIGNACAO_REDE = @DESIGNACAO 
                OR
                    NOME_DOMINIO_REDE = @DOMINIO;
            END
        COMMIT TRANSACTION;

        SELECT
            NovoID
        FROM
            @OutputID;
    END TRY
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION;
        EXEC sp_TratarErro;
        THROW;
    END CATCH
END;

/*
EXEC sp_InserirRede
    @DESIGNACAO = 'Instagram',
    @DOMINIO = 'instagram.com',
    @DiscenteCursoInstituicaoID = 'x';
*/